# Future events with distinct name, place and date

PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT REDUCED  ?start  ?label ?description  ?venue ?image ?locality ?country
WHERE {
  ?e a schema:Event ; 
  	schema:startDate ?start ;
    schema:location/schema:address/schema:addressLocality ?locality
  
  filter(?start > '2020-06-05T19:01:52.798Z'^^xsd:dateTime)
  
	
  OPTIONAL {   ?e schema:name ?label_en   filter (lang(?label_en) = "en")} .
  OPTIONAL {   ?e schema:name   ?label_fr  filter (lang(?label_fr) = "fr")} .
  OPTIONAL {   ?e schema:name  ?label_plain  filter (lang(?label_plain) = "") } .
  BIND (coalesce (?label_plain, ?label_en, ?label_fr) as ?label)
  
  OPTIONAL { ?e schema:description ?description }
  
  ?e schema:location/schema:name ?venue
  filter (lang(?venue) = '')
  OPTIONAL { ?e schema:image ?image }

  values ?country {"Canada"}

} 
ORDER BY ?start ?label 
LIMIT 1000