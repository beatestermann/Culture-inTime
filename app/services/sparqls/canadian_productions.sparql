# Productions from Artsdata.ca

PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT REDUCED ?production_uri ?start  ?label ?description  ?venue ?image ?locality ?country 

WHERE {
  ?production_uri a schema:EventSeries ;
  	schema:startDate ?start ;
    schema:location/schema:address/schema:addressLocality ?locality .
  
  filter(?start > '2016-08-01T08:00:00Z'^^xsd:dateTime)
  
  # label
  OPTIONAL { ?production_uri schema:name ?label_en . filter (lang(?label_en) = "en") } .
  OPTIONAL { ?production_uri schema:name ?label_fr .  filter (lang(?label_fr) = "fr") } .
  OPTIONAL { ?production_uri schema:name ?label_plain .  filter (lang(?label_plain) = "") } .
  BIND (coalesce (?label_plain, ?label_en, ?label_fr) as ?label)
    
  # description
  OPTIONAL { ?production_uri ^schema:superEvent/schema:description ?description_plain filter (lang(?description_plain) = "")}
  OPTIONAL { ?production_uri ^schema:superEvent/schema:description ?description_en  filter (lang(?description_en) = "en")}
  OPTIONAL { ?production_uri ^schema:superEvent/schema:description ?description_fr  filter (lang(?description_fr) = "fr")}
  BIND (coalesce (?description_plain, ?description_en, ?description_fr) as ?description)
  
  # venue
  ?production_uri schema:location ?location .
  { graph <http://kg.artsdata.ca/Place>  
    {
      ?location schema:name ?venue
      filter (lang(?venue) = '')
    }
  }

  # image
  OPTIONAL { ?production_uri ^schema:superEvent/schema:image ?image }

  # country
  values ?country {"Canada"}

} 
ORDER BY ?start ?label 
LIMIT 2000