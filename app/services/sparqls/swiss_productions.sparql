PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?production_uri ?label ?venue ?start ?description ?locality ?country ?production_company_uri ?production_company_label

WHERE { 
  
  SERVICE <https://query.wikidata.org/sparql>
    { ?production_uri (wdt:P31/wdt:P279*) wd:Q43099500 ;
        wdt:P276*/wdt:P131 wd:Q72 ;
        wdt:P1191 ?start  .
        
      # label
      OPTIONAL { ?production_uri  rdfs:label ?label_en .  filter (lang(?label_en) = "en")  }
      OPTIONAL { ?production_uri  rdfs:label ?label_de .  filter (lang(?label_de) = "de")  }
      OPTIONAL { ?production_uri  rdfs:label ?label_fr .  filter (lang(?label_fr) = "fr")  }
      BIND (coalesce (?label_en, ?label_de, ?label_fr) as ?label)
      
      # description
      ?production_uri schema:description ?description .
      filter (lang(?description) = "en") 
      
      # venue
      ?production_uri wdt:P276/rdfs:label ?venue
      filter (lang(?venue) = "en") 

      # production company
      OPTIONAL { ?production_uri wdt:P272 ?production_company_uri . }  
      OPTIONAL { ?production_company_uri rdfs:label  ?production_company_label . filter (lang(?production_company_label) = "en")  }

      # city
      values ?locality {"ZÃ¼rich"}

      # country
      values ?country {"Switzerland"}
    }
} 
LIMIT 1000